USE [QLDSV_TC]
GO

CREATE PROCEDURE [dbo].[sp_DangKyLopTinChi]
	@MALTC int, @MASV nchar(10), @MAMH nchar(10), @HOCKY int, @NIENKHOA nchar(9)
AS
BEGIN
	SELECT DK.MALTC 
	FROM DANGKY DK
	LEFT JOIN LOPTINCHI LTC
	ON  DK.MALTC = LTC.MALTC
	WHERE	LTC.MAMH = @MAMH AND 
			LTC.NIENKHOA = @NIENKHOA AND 
			LTC.HOCKY = @HOCKY  AND 
			DK.HUYDANGKY = 0 AND 
			DK.MASV = @MASV
	
	IF @@ROWCOUNT > 0
	BEGIN
		RAISERROR ('Môn học đã đăng ký rồi!', 16,1)
		RETURN 1;
	END


	UPDATE DANGKY 
	SET    HUYDANGKY = 0 
	WHERE  MALTC = @MALTC AND MASV = @MASV 

	IF @@ROWCOUNT = 0 
	  INSERT INTO DANGKY (MALTC, MASV, DIEM_CC, DIEM_GK, DIEM_CK, HUYDANGKY) 
	  VALUES (@MALTC, @MASV , 0, 0, 0, 0)
END

GO





CREATE PROCEDURE [dbo].[sp_Get_Info_Login]
	@LOGINNAME varchar(50)
AS
BEGIN
	DECLARE @TENUSER NVARCHAR(50)
	SELECT @TENUSER=NAME FROM sys.sysusers WHERE sid = SUSER_SID(@LOGINNAME)
 
	DECLARE @HOTEN NVARCHAR(50)
	if exists (SELECT HO+ ' '+ TEN FROM GIANGVIEN  WHERE MAGV =  @TENUSER)
	SELECT @HOTEN=HO+ ' '+ TEN FROM GIANGVIEN  WHERE MAGV =  @TENUSER

	SELECT USERNAME = @TENUSER, HOTEN = @HOTEN, TENNHOM = NAME
	   FROM sys.sysusers 
	   WHERE UID = (SELECT GROUPUID 
					FROM SYS.SYSMEMBERS 
					WHERE MEMBERUID = (SELECT UID FROM sys.sysusers WHERE NAME = @TENUSER)
					)
END

GO


CREATE PROCEDURE [dbo].[sp_GetBangDiemMonHoc]
	-- khai bao cac bien tam 
	@MALTC int 
AS
BEGIN

	SELECT SV.MASV, SV.HO + ' '+ sv.TEN as 'HOTEN', DK.DIEM_CC, DK.DIEM_GK AS DIEM_GK, DK.DIEM_CK AS DIEM_CK
	FROM (SELECT MASV, MALTC, DIEM_CC, DIEM_GK, DIEM_CK FROM DANGKY WHERE MALTC = @MALTC AND HUYDANGKY = 0) as DK, SINHVIEN SV
	WHERE SV.MASV = DK.MASV AND SV.DANGHIHOC = 0
	ORDER BY SV.TEN, SV.HO
	  
END

GO


CREATE PROCEDURE [dbo].[sp_GetChiTietDongHocPhi]
	-- khai bao cac bien tam 
	@MASV nchar(10), @NIENKHOA nchar(9), @HOCKY int
AS
BEGIN

	SELECT CT_DONGHOCPHI.NGAYDONG, CT_DONGHOCPHI.SOTIENDONG
	FROM CT_DONGHOCPHI
	WHERE CT_DONGHOCPHI.NIENKHOA = @NIENKHOA AND CT_DONGHOCPHI.HOCKY = @HOCKY AND CT_DONGHOCPHI.MASV = @MASV
	  
END

GO



CREATE PROCEDURE [dbo].[sp_GetDS_LTC]
	-- khai bao cac bien tam 
	@NIENKHOA nchar(9), @HOCKY int
AS
BEGIN

	SELECT MALTC, TENMH, NHOM, HO + ' ' + TEN AS 'HOTEN'
	FROM (SELECT MALTC, MAMH, NHOM, MAGV FROM LOPTINCHI WHERE NIENKHOA = @NIENKHOA AND HOCKY = @HOCKY AND HUYLOP = 0) LTC, MONHOC MH, GIANGVIEN GV
	WHERE LTC.MAMH = MH.MAMH AND LTC.MAGV = GV.MAGV
	ORDER BY TENMH, NHOM
END

GO


CREATE PROCEDURE [dbo].[sp_GetThongTinDongHocPhi]
	-- khai bao cac bien tam 
	@MASV nchar(10)
AS
BEGIN
	SELECT DISTINCT HP.MASV, HP.NIENKHOA, HP.HOCKY, HP.HOCPHI, ISNULL(CT.SOTIENDADONG, 0) as SOTIENDADONG
	FROM (SELECT NIENKHOA, HOCKY, HOCPHI, MASV FROM HOCPHI WHERE MASV = @MASV) as HP

	LEFT JOIN (
				SELECT MASV, NIENKHOA, HOCKY, SUM(SOTIENDONG) AS SOTIENDADONG 
				FROM CT_DONGHOCPHI GROUP BY NIENKHOA, HOCKY, MASV
			) AS CT
	ON HP.MASV = CT.MASV AND CT.NIENKHOA = HP.NIENKHOA AND CT.HOCKY = HP.HOCKY
END

GO


CREATE PROCEDURE [dbo].[sp_GhiDiem]
	-- khai bao cac bien tam 
	@DIEMTHI TYPE_DANGKY READONLY
AS
BEGIN
	MERGE INTO DANGKY AS Target
	USING (SELECT MALTC, MASV, DIEM_CC, DIEM_GK, DIEM_CK FROM @DIEMTHI) AS Source
	ON Target.MALTC = Source.MALTC AND Target.MASV = Source.MASV
	WHEN MATCHED THEN 
		UPDATE SET Target.DIEM_CC = Source.DIEM_CC, Target.DIEM_GK = ROUND(Source.DIEM_GK,2), Target.DIEM_CK = ROUND(Source.DIEM_CK,2)

	WHEN NOT MATCHED THEN
		INSERT(MALTC, MASV, DIEM_CC, DIEM_GK, DIEM_CK)
		VALUES(Source.MALTC, Source.MASV, Source.DIEM_CC, ROUND(Source.DIEM_GK,2), ROUND(Source.DIEM_CK,2));
END

GO

CREATE PROCEDURE [dbo].[sp_InBangDiemMonHoc] @NIENKHOA nchar(9), @HOCKY int, @MAMH nchar(10), @NHOM int 
AS
BEGIN

	SELECT ROW_NUMBER() over(ORDER BY sv.TEN, sv.HO) STT, sv.MASV, sv.HO, sv.TEN, dk.DIEM_CC, dk.DIEM_GK, dk.DIEM_CK, (dk.DIEM_CC *0.1 +dk.DIEM_GK*0.3 + dk.DIEM_CK * 0.6) as 'DIEM_HM'
	FROM (SELECT ltc.MALTC from LOPTINCHI as ltc where ltc.MAMH = @MAMH AND ltc.NIENKHOA = @NIENKHOA AND ltc.HOCKY = @HOCKY AND ltc.NHOM = @NHOM) as ltc

	INNER JOIN DANGKY as dk ON dk.MALTC= ltc.MALTC
	INNER JOIN SINHVIEN as sv ON dk.MASV = sv.MASV

	WHERE sv.DANGHIHOC = 'False'

	ORDER BY sv.TEN, sv.HO
	  
END

GO

CREATE   PROCEDURE [dbo].[sp_InBangDiemTongKet]
	-- khai bao cac bien tam 
	@MALOP nchar(10)
AS
BEGIN
	SELECT HOTEN = SV.MASV + ' - ' +SV.HO + ' ' + SV.TEN, DIEM.TENMH, DIEM.DIEM
FROM SINHVIEN AS SV
INNER JOIN (SELECT MASV, DIEM = MAX(DIEM), TENMH
			FROM 
			(SELECT DK.MALTC, DK.MASV, DIEM = DK.DIEM_CC *0.1 + DK.DIEM_CK * 0.6 + DK.DIEM_GK * 0.3 
			FROM DANGKY DK
			GROUP BY DK.MALTC, DK.MASV, DK.DIEM_CC, DK.DIEM_CK, DK.DIEM_GK) AS DIEM

			LEFT JOIN  (
				SELECT LTC.MALTC, MH.TENMH
				FROM MONHOC MH
				INNER JOIN LOPTINCHI LTC
				ON LTC.MAMH = MH.MAMH
			) AS MH

			ON DIEM.MALTC = MH.MALTC
			GROUP BY MH.TENMH, DIEM.MASV) AS DIEM

ON SV.MASV = DIEM.MASV  AND SV.MALOP = @MALOP
GROUP BY sv.HO, sv.MASV, sv.TEN, DIEM.TENMH, DIEM.DIEM
ORDER BY sv.TEN, sv.HO ASC

END
GO


CREATE PROCEDURE [dbo].[sp_InDSDongHocPhi]
	-- khai bao cac bien tam 
	@MALOP nchar(10), @NIENKHOA nchar(9), @HOCKY int
AS
BEGIN
	SELECT (hs.HO + ' ' + hs.TEN) as 'HO TEN', hs.HOCPHI, ISNULL(CT.SOTIENDADONG, 0) AS SOTIENDADONG
	FROM (

		(	SELECT hp.MASV, sv.HO, sv.TEN, hp.NIENKHOA, hp.HOCKY, hp.HOCPHI
			FROM (SELECT * FROM HOCPHI WHERE NIENKHOA = @NIENKHOA AND HOCKY = @HOCKY ) AS hp, 
				 (SELECT MASV, HO, TEN FROM SINHVIEN WHERE MALOP = @MALOP) AS sv
			WHERE hp.MASV = sv.MASV 
		 ) AS hs

		 LEFT JOIN (
			SELECT MASV, HOCKY, NIENKHOA, SUM(SOTIENDONG) AS SOTIENDADONG
			FROM CT_DONGHOCPHI 
			GROUP BY MASV, HOCKY, NIENKHOA
		 ) AS CT
		 ON CT.MASV = HS.MASV AND CT.NIENKHOA = HS.NIENKHOA AND CT.HOCKY = HS.HOCKY
	)
	  
END

GO


CREATE PROCEDURE [dbo].[sp_InDSLTC] @NIENKHOA nchar(9), @HOCKY int
AS
BEGIN

	SELECT  ROW_NUMBER() over(ORDER BY mh.TENMH, ltc.NHOM) STT, mh.TENMH, ltc.NHOM, (gv.HO + ' ' + gv.TEN) as 'HO TEN', ltc.SOSVTOITHIEU, COUNT(dk.MASV) as soSVDK
	
	FROM  (SELECT ltc.MALTC, ltc.NIENKHOA, ltc.HOCKY, ltc.nhom, ltc.SOSVTOITHIEU , ltc.MAGV , ltc.MAMH , ltc.HUYLOP FROM LOPTINCHI as ltc WHERE ltc.NIENKHOA = @NIENKHOA AND ltc.HOCKY = @HOCKY AND ltc.HUYLOP = 0) as ltc

	LEFT JOIN GIANGVIEN as gv ON gv.MAGV = ltc.MAGV
	LEFT JOIN MONHOC as mh ON mh.MAMH = ltc.MAMH
	LEFT JOIN DANGKY as dk ON dk.MALTC = ltc.MALTC

	GROUP BY ltc.NHOM, mh.TENMH, gv.HO, gv.TEN, ltc.SOSVTOITHIEU
	ORDER BY mh.TENMH, ltc.NHOM
	  
END

GO


CREATE PROCEDURE [dbo].[sp_InDSSV]
	-- Add the parameters for the stored procedure here
	@NIENKHOA nchar(9), @HOCKY int, @MAMH nchar(10), @NHOM int
AS
BEGIN

	SELECT  ROW_NUMBER() over(ORDER BY sv.TEN, sv.HO) STT, sv.MASV, sv.HO, sv.TEN, Case 
            When sv.PHAI='false' Then 'Nam' 
            Else 'Nu' END AS Phai  
			, sv.MALOP
	FROM  (SELECT ltc.MALTC from LOPTINCHI as ltc where ltc.MAMH = @MAMH AND ltc.NIENKHOA = @NIENKHOA AND ltc.HOCKY = @HOCKY AND ltc.NHOM = @NHOM) as ltc
	Inner join DANGKY as dk  on dk.MALTC = ltc.MALTC
	Inner join SINHVIEN as sv on dk.MASV = sv.MASV
	WHERE  sv.DANGHIHOC = 'False'

	ORDER BY sv.TEN, sv.HO

END
GO


CREATE PROCEDURE [dbo].[sp_KiemTraHocPhi]
	-- khai bao cac bien tam 
	@MASV nchar(10), @NIENKHOA nchar(9), @HOCKY int
AS
BEGIN
	IF EXISTS ( SELECT 1 FROM HOCPHI WHERE MASV = @MASV AND NIENKHOA = @NIENKHOA AND HOCKY = @HOCKY)
	BEGIN
		RAISERROR ('Thông tin học phí đã tồn tại', 16,1)
		RETURN 1;
	END
END

GO


CREATE  PROCEDURE [dbo].[sp_KiemTraHocPhiDaDong]
	-- khai bao cac bien tam 
	@MASV nchar(10), @NIENKHOA nchar(9), @HOCKY int, @NGAYDONG date
AS
BEGIN
	IF EXISTS ( SELECT 1 FROM CT_DONGHOCPHI WHERE MASV = @MASV AND NIENKHOA = @NIENKHOA AND HOCKY = @HOCKY AND NGAYDONG = @NGAYDONG)
	BEGIN
		RAISERROR ('Sinh viên đã đóng tiền vào ngày này. Vui lòng chọn ngày khác', 16,1)
		RETURN 1;
	END
END
GO


CREATE PROC [dbo].[sp_KiemTraLTC]
@NIENKHOA nchar(9), @HOCKY int, @MAMH nchar(10), @NHOM int 
AS
BEGIN
	IF EXISTS ( SELECT * FROM LOPTINCHI WHERE LOPTINCHI.NIENKHOA = @NIENKHOA AND LOPTINCHI.HOCKY = @HOCKY AND LOPTINCHI.MAMH = @MAMH AND LOPTINCHI.NHOM = @NHOM)
	BEGIN
		RAISERROR ('Lớp tín chỉ đã tồn tại', 16,1)
		RETURN 1;
	END


	IF EXISTS ( SELECT * FROM LINK0.QLDSV_TC.DBO.LOPTINCHI WHERE LOPTINCHI.NIENKHOA = @NIENKHOA AND LOPTINCHI.HOCKY = @HOCKY AND LOPTINCHI.MAMH = @MAMH AND LOPTINCHI.NHOM = @NHOM)
	BEGIN
		RAISERROR ('Lớp tín chỉ đã tồn tại trên chi nhánh khác', 16,1)
		RETURN 1;
	END

RETURN 0;
END
GO


CREATE PROC [dbo].[sp_KiemTraMaGV]
@MAGV char(15), @MAKHOA char(10)
AS
BEGIN
	IF NOT EXISTS  ( SELECT * FROM GIANGVIEN WHERE GIANGVIEN.MAGV = @MAGV AND GIANGVIEN.MAKHOA = @MAKHOA)
	BEGIN
		RAISERROR ('Mã giảng viên không tồn tại trên khoa đang chọn', 16,1)
		RETURN 1;
	END

RETURN 0;
END
GO


CREATE PROC [dbo].[sp_KiemTraMaLop]
@MALOP char(10)
AS
BEGIN
	IF EXISTS ( SELECT * FROM LOP WHERE LOP.MALOP = @MALOP)
	BEGIN
		RAISERROR ('Mã lớp học đã tồn tại', 16,1)
		RETURN 1;
	END

	IF EXISTS ( SELECT * FROM LINK0.QLDSV_TC.DBO.LOP WHERE LOP.MALOP = @MALOP)
	BEGIN
		RAISERROR ('Mã lớp học đã tồn tại trên chi nhánh khác', 16,1)
		RETURN 1;
	END

RETURN 0;
END
GO


CREATE PROC [dbo].[sp_KiemTraMaMH]
@MAMH char(15)
AS
BEGIN
	IF EXISTS ( SELECT * FROM MONHOC WHERE MONHOC.MAMH = @MAMH)
	BEGIN
		RAISERROR ('Mã môn học đã tồn tại', 16,1)
		RETURN 1;
	END

	

	IF EXISTS ( SELECT * FROM LINK0.QLDSV_TC.DBO.MONHOC WHERE MONHOC.MAMH = @MAMH)
	BEGIN
		RAISERROR ('Mã môn học đã tồn tại trên chi nhánh khác', 16,3)
		RETURN 1;
	END

RETURN 0;
END
GO

CREATE PROCEDURE [dbo].[sp_KiemTraMaSV]
	-- Add the parameters for the stored procedure here
	@MASV nchar(10)
AS
BEGIN
	IF EXISTS ( SELECT * FROM SINHVIEN as sv WHERE sv.MASV = @MASV )
		BEGIN
			RAISERROR ('Mã sinh viên đã tồn tại', 16,1)
			RETURN 1;
		END

		

		IF EXISTS ( SELECT * FROM LINK0.QLDSV_TC.DBO.SINHVIEN as sv WHERE sv.MASV = @MASV)
		BEGIN
			RAISERROR ('Mã sinh viên đã tồn tại ở Khoa khác', 16,1)
			RETURN 1;
		END

	RETURN 0;
END

GO


CREATE PROCEDURE [dbo].[sp_LayDSLopTinChiDaDangKy]
	-- khai bao cac bien tam 
	@MASV nchar(10), @NIENKHOA nchar(9), @HOCKY int
AS
BEGIN

	SELECT ROW_NUMBER() over(ORDER BY MH.TENMH, LTC.NHOM) STT,LTC.MALTC, MH.MAMH, MH.TENMH, LTC.NHOM
	FROM  LOPTINCHI as LTC

	INNER JOIN MONHOC as MH ON MH.MAMH = LTC.MAMH
	INNER JOIN (SELECT MASV, MALTC FROM DANGKY WHERE MASV = @MASV AND HUYDANGKY = 0) as DK ON DK.MALTC = LTC.MALTC

	WHERE LTC.NIENKHOA = @NIENKHOA AND LTC.HOCKY = @HOCKY AND LTC.HUYLOP = 0 
	GROUP BY LTC.NHOM, MH.MAMH, MH.TENMH, LTC.MALTC
	ORDER BY MH.TENMH, LTC.NHOM
	  
END

GO


CREATE PROCEDURE [dbo].[sp_LayDSLopTinChiDeDangKy]
	-- khai bao cac bien tam 
	@NIENKHOA nchar(9), @HOCKY int
AS
BEGIN

	SELECT ltc.MALTC, mh.MAMH, mh.TENMH, ltc.NHOM, (gv.HO + ' ' + gv.TEN) as 'HO TEN', ltc.SOSVTOITHIEU, COUNT(dk.MASV) as SOSVDK
	FROM  LOPTINCHI as ltc

	LEFT JOIN GIANGVIEN as gv ON gv.MAGV = ltc.MAGV
	LEFT JOIN MONHOC as mh ON mh.MAMH = ltc.MAMH
	LEFT JOIN (SELECT MASV, MALTC FROM DANGKY WHERE HUYDANGKY = 0) as dk ON dk.MALTC = ltc.MALTC

	WHERE ltc.NIENKHOA = @NIENKHOA AND ltc.HOCKY = @HOCKY AND ltc.HUYLOP = 0
	GROUP BY ltc.MALTC, ltc.NHOM, mh.MAMH, mh.TENMH, gv.HO, gv.TEN, ltc.SOSVTOITHIEU
	ORDER BY mh.TENMH, ltc.NHOM
	  
END

GO


CREATE PROCEDURE [dbo].[sp_PhieuDiem]
	-- khai bao cac bien 
	@MASV char(12)
AS
BEGIN
   SELECT  ROW_NUMBER() over(ORDER BY mh.TENMH) STT, mh.TENMH , MAX(dk.DIEM_CC *0.1 + dk.DIEM_CK * 0.6 + dk.DIEM_GK * 0.3) AS 'DIEM'
   
   FROM (SELECT * from DANGKY as dk where dk.MASV = @MASV ) as dk
	
   INNER JOIN LOPTINCHI as ltc ON dk.MALTC= ltc.MALTC
   INNER JOIN MONHOC as mh ON mh.MAMH = ltc.MAMH
   

   GROUP BY mh.TENMH
   ORDER BY mh.TENMH
END
GO


CREATE   PROCEDURE [dbo].[sp_SuaChiTietDongHP]
	-- khai bao cac bien tam 
	@MASV nchar(10), 
	@NIENKHOA nchar(9), @HOCKY int,  @NGAYDONG date,
	@NIENKHOANEW nchar(9), @HOCKYNEW  int, @SOTIENDONGNEW int, @NGAYDONGNEW date
AS
BEGIN

	DECLARE @TONGTIENDADONG int
	DECLARE @HOCPHI int

	SET XACT_ABORT ON

	BEGIN TRY
		BEGIN TRANSACTION

		UPDATE CT_DONGHOCPHI
		SET NGAYDONG = @NGAYDONGNEW, SOTIENDONG = @SOTIENDONGNEW
		WHERE MASV = @MASV AND NIENKHOA = @NIENKHOA AND HOCKY = @HOCKY AND NGAYDONG = @NGAYDONG
		

		SELECT @HOCPHI = HP.HOCPHI, @TONGTIENDADONG = ISNULL(CT.SOTIENDADONG, 0)
		FROM (SELECT NIENKHOA, HOCKY, HOCPHI, MASV FROM HOCPHI WHERE MASV = @MASV) as HP
		LEFT JOIN (
					SELECT MASV, NIENKHOA, HOCKY, SUM(SOTIENDONG) AS SOTIENDADONG 
					FROM CT_DONGHOCPHI GROUP BY NIENKHOA, HOCKY, MASV
				) AS CT
		ON HP.MASV = CT.MASV AND CT.NIENKHOA = HP.NIENKHOA AND CT.HOCKY = HP.HOCKY

		
		SELECT @TONGTIENDADONG = SUM(SOTIENDONG)
		FROM CT_DONGHOCPHI CT
		WHERE CT.MASV = @MASV AND CT.NIENKHOA = @NIENKHOA AND CT.HOCKY = @HOCKY 
	END TRY

	BEGIN CATCH
		IF (@@TRANCOUNT > 0)
		BEGIN
			ROLLBACK TRANSACTION
			RAISERROR ('Lỗi hệ thống!', 16,2)
			RETURN -1;
		END
	END CATCH
	  
	IF(@TONGTIENDADONG > @HOCPHI)
	BEGIN
		ROLLBACK TRANSACTION
		RAISERROR ('Số tiền cần sửa vượt quá tiền học phí. Ko thể cập nhật!', 16,1)
		RETURN 1;
	END

	IF(@@TRANCOUNT > 0)
	BEGIN
		COMMIT TRANSACTION
		RETURN 0	--Thành công
	END
END
GO


CREATE PROCEDURE [dbo].[sp_SuaHocPhi]
	-- khai bao cac bien tam 
	@MASV nchar(10), 
	@NIENKHOA nchar(9), @HOCKY int, @HOCPHI int, 
	@NIENKHOANEW nchar(9), @HOCKYNEW  int, @HOCPHINEW  int
AS
BEGIN
	UPDATE HOCPHI
	SET  NIENKHOA = @NIENKHOANEW, HOCKY = @HOCKYNEW, HOCPHI = @HOCPHINEW
	WHERE MASV = @MASV AND NIENKHOA = @NIENKHOA AND HOCKY = @HOCKY 
END

GO


CREATE PROCEDURE [dbo].[sp_TaoTaiKhoan]
	@USERNAME VARCHAR(50),  @PASSWORD VARCHAR(50),
    @USERID VARCHAR(50),  @ROLE VARCHAR(50)    
AS

	DECLARE @RET INT

	EXEC @RET= SP_ADDLOGIN @USERNAME, @PASSWORD, 'QLDSV_TC'
	IF (@RET = 1)  -- USERNAME BI TRUNG
		BEGIN
			RETURN 1 -- Username đã tồn tại
		END 

  EXEC @RET= SP_GRANTDBACCESS @USERNAME, @USERID
  IF (@RET = 1)  -- USERID BI TRUNG
	  BEGIN
		   EXEC SP_DROPLOGIN @USERNAME
		   RETURN 2 -- UserId đã tồn tại tài khoản đăng nhập
	  END


  EXEC sp_addrolemember @ROLE, @USERID
  IF @ROLE <> 'SV'
	  BEGIN
		  EXEC sp_addsrvrolemember @USERNAME, 'SecurityAdmin'
	  END

RETURN 0 -- tạo thành công

GO


CREATE   PROCEDURE [dbo].[sp_ThemChiTietDongHP]
	-- khai bao cac bien tam 
	@MASV nchar(10), @NIENKHOA nchar(9), @HOCKY int, @NGAYDONG date, @SOTIENDONG int 
AS
BEGIN

	DECLARE @TONGTIENDADONG int
	DECLARE @HOCPHI int

	SET XACT_ABORT ON

	BEGIN TRY
		BEGIN TRANSACTION

		INSERT INTO CT_DONGHOCPHI(MASV, NIENKHOA , HOCKY, NGAYDONG, SOTIENDONG)
		VALUES (@MASV, @NIENKHOA, @HOCKY, @NGAYDONG, @SOTIENDONG)
		

		SELECT @HOCPHI = HP.HOCPHI, @TONGTIENDADONG = ISNULL(CT.SOTIENDADONG, 0)
		FROM (SELECT NIENKHOA, HOCKY, HOCPHI, MASV FROM HOCPHI WHERE MASV = @MASV) as HP
		LEFT JOIN (
					SELECT MASV, NIENKHOA, HOCKY, SUM(SOTIENDONG) AS SOTIENDADONG 
					FROM CT_DONGHOCPHI GROUP BY NIENKHOA, HOCKY, MASV
				) AS CT
		ON HP.MASV = CT.MASV AND CT.NIENKHOA = HP.NIENKHOA AND CT.HOCKY = HP.HOCKY

		
		SELECT @TONGTIENDADONG = SUM(SOTIENDONG)
		FROM CT_DONGHOCPHI CT
		WHERE CT.MASV = @MASV AND CT.NIENKHOA = @NIENKHOA AND CT.HOCKY = @HOCKY 
	END TRY

	BEGIN CATCH
		IF (@@TRANCOUNT > 0)
		BEGIN
			ROLLBACK TRANSACTION
			RAISERROR ('Lỗi hệ thống!', 16,2)
			RETURN -1;
		END
	END CATCH

	IF(@TONGTIENDADONG > @HOCPHI)
	BEGIN
		ROLLBACK TRANSACTION
			
		RAISERROR ('Số tiền thêm vượt quá tiền học phí. Ko thể thêm!', 16,1)
		RETURN 1;
	END

	IF(@@TRANCOUNT > 0)
	BEGIN
		COMMIT TRANSACTION
		RETURN 0	--Thành công
	END
	  
END
GO


CREATE PROCEDURE [dbo].[sp_ThemHocPhi]
	-- khai bao cac bien tam 
	@MASV nchar(10), @NIENKHOA nchar(9), @HOCKY int, @HOCPHI int 
AS
BEGIN
	INSERT INTO HOCPHI(MASV, NIENKHOA , HOCKY, HOCPHI)
	VALUES (@MASV, @NIENKHOA, @HOCKY, @HOCPHI)
END

GO


CREATE  PROCEDURE [dbo].[sp_XoaChiTietDongHP]
	-- khai bao cac bien tam 
	@MASV nchar(10), @NIENKHOA nchar(9), @HOCKY int, @NGAYDONG date
AS
BEGIN
	DELETE FROM CT_DONGHOCPHI
	WHERE MASV = @MASV AND NIENKHOA = @NIENKHOA AND HOCKY = @HOCKY AND NGAYDONG = @NGAYDONG
END
GO


CREATE PROCEDURE [dbo].[sp_XoaHocPhi]
	-- khai bao cac bien tam 
	@MASV nchar(10), @NIENKHOA nchar(9), @HOCKY int
AS
BEGIN
	DELETE FROM HOCPHI
	WHERE MASV = @MASV AND NIENKHOA = @NIENKHOA AND HOCKY = @HOCKY
END
GO